export setup = (servers, options) ->
  lspconfig = require 'lspconfig'
  icons = require 'my.icons'

  require'mason'.setup {
    ui:
      icons:
        package_installed: icons.server_installed
        package_pending: icons.server_pending
        package_uninstalled: icons.server_uninstalled
  }

  require'mason-tool-installer'.setup {
    ensure_installed: { 'codelldb', 'stylua', 'shfmt', 'shellcheck', 'black', 'isort', 'prettierd' }
    auto_update: false
    run_on_start: true
  }

  require'mason-lspconfig'.setup {
    ensure_installed: vim.tbl_keys servers
    automatic_installation: false
  }

  -- Package installation folder
  install_root_dir = vim.fn.stdpath('data') .. '/mason'

  require('mason-lspconfig').setup_handlers {
    (server_name) ->
      opts = vim.tbl_deep_extend('force', options, servers[server_name] or {})
      opts.capabilities.offset_encoding = 'utf-8'
      opts.offset_encoding = 'utf-8'
      lspconfig[server_name].setup { ...opts }
    ['rust_analyzer']: ->
      opts = vim.tbl_deep_extend('force', options, servers['rust_analyzer'] or {})

      -- DAP settings - https://github.com/simrat39/rust-tools.nvim#a-better-debugging-experience
      extension_path = install_root_dir .. '/packages/codelldb/extension/'
      codelldb_path = extension_path .. 'adapter/codelldb'
      liblldb_path = extension_path .. 'lldb/lib/liblldb.so'
      require('rust-tools').setup {
        tools:
          autoSetHints: false
          executor: require('rust-tools/executors').toggleterm
          hover_actions: { border: 'solid' }
          on_initialized: ->
            vim.api.nvim_create_autocmd({ 'BufWritePost', 'BufEnter', 'CursorHold', 'InsertLeave' }, {
              pattern: { '*.rs' }
              callback: -> vim.lsp.codelens.refresh()
            })
        server: opts
        dap:
          adapter: require('rust-tools.dap').get_codelldb_adapter(codelldb_path, liblldb_path)
      }
    ['tsserver']: ->
      opts = vim.tbl_deep_extend('force', options, servers['tsserver'] or {})
      require('typescript').setup {
        disable_commands: false
        debug: false
        server: opts
      }
  }
